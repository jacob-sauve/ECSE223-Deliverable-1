class FashionStoreManagementApp {
  
}

class User {
  lazy name;
  lazy phoneNumber;
}

class AccountType {
  abstract;
  unique immutable String username;
  password;
}

class Employee {
  isA AccountType;
}

class Customer {
  isA AccountType;
  Integer loyaltyPoints;
}

class Manager {
  isA AccountType;
  String username="manager";
  String password="manager";
}

class Address {
  addressLineOne;
  postalCode;
  city;
  country;
}

class ClothingItem {
  unique String name;
  Double price;
  Integer loyaltyPoints;
  [loyaltyPoints >= 1 && loyaltyPoints <= 5]
}

class LimitedItem {
  isA ClothingItem;
  Date startDate;
  Date endDate;
  // if is seasonal, kept in system with shifted start/end dates
  Boolean isSeasonal;
}

class InventoryItem {
  Integer quantity;
  Size size;
}

class CartItem {
  Integer quantity;
  Size size;
}

class Cart {
  
}

class Order {
  unique Integer orderNumber;
  Integer shippingDelay;
  [shippingDelay >= 0 && shippingDelay <= 3]
}

class Shipment {
    Boolean shipped;
    Date shippingDate;
}

class ShipmentItem {
  Size size;
  Integer quantity;
}

enum Size {S, M, L, XL}

association {
  1 Customer -- 1..* Address shippingAddresses;
// only 1 manager account per system
  1 Manager storeManager -<@> 1 FashionStoreManagementApp system;
// to prevent having to instantiate AccountType (default case)
  1 User person -- 0..3 AccountType storeRoles;
// all sizes added/removed at once to inventory
  1 ClothingItem catalogItem -- 4 InventoryItem stockedSizes;
  //1 Inventory storeInventory -- * InventoryItem stockedItems;
  1 Customer -- 1 Cart;
  1 Customer -- * Order paidOrders;
  1 Manager -- * Order ordersToAssign;
  1 Employee itemGatherer -- * Order ordersToAssemble;
  1 Cart paidCart -- 0..1 Order placedOrder;
  1 Cart <@>- * CartItem;
  0..4 CartItem -- 1 ClothingItem;
// all items offered by the store, including those out of stock
  * ClothingItem catalogItems -<@> 1 FashionStoreManagementApp catalog;
// all sizes in stock and their quantities
  * InventoryItem stockedItems -<@> 0..1 FashionStoreManagementApp inventory;
  1 Shipment orderedShipment <@>- * ShipmentItem shippedItems;
  1 ClothingItem itemTypeToShip -- 0..4 ShipmentItem specificReceivedItems;
  1 Manager shipmentManager -- * Shipment shipments;
  1 Manager storeManager -- * Employee managedEmployees;
  1 Address deliveryAddress -- * Order;
// Users and the app have tied lifecycles
  1 FashionStoreManagementApp system -- * User users;
}